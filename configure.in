AC_INIT(src/Makefile.am)

. $srcdir/OAESIS-VERSION

dnl libtool versioning
LT_RELEASE=$OAESIS_MAJOR_VERSION.$OAESIS_MINOR_VERSION
LT_CURRENT=`expr $OAESIS_MICRO_VERSION - $OAESIS_INTERFACE_AGE`
LT_REVISION=$OAESIS_INTERFACE_AGE
LT_AGE=`expr $OAESIS_BINARY_AGE - $OAESIS_INTERFACE_AGE`
LT_VERSION_INFO="-version-info $LT_CURRENT:$LT_REVISION:$LT_AGE"

dnl Check system type
AC_CANONICAL_SYSTEM

dnl Initialize automake macros
AM_INIT_AUTOMAKE($PACKAGE, $VERSION, no-define)

dnl Make a header based on the things we will find out
AM_CONFIG_HEADER(config.h)

dnl Use libtool to create liboaesis
AM_PROG_LIBTOOL

dnl Check for programs
AC_ARG_PROGRAM
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_MAKE_SET
AC_PROG_RANLIB

if test x$with_ovdisis = xyes ; then
  AC_MSG_ERROR([
*** Directory must be specified for --with-ovdisis])
fi

if test x$with_ovdisis = x ; then 
  # Look for separately installed ovdisis

  AM_PATH_OVDISIS(0.0.1,,
    AC_MSG_ERROR([
*** ovdisis 0.0.1 or better is required. The latest version of ovdisis
*** is always available from ftp://ftp.nocrew.org.]),
    libovdisis)

  ovdisis_cflags=$OVDISIS_CFLAGS
  ovdisis_libs=$OVDISIS_LIBS
else
  # Use uninstalled ovdisis (assume they got the version right)

  if test -x $with_ovdisis/ovdisis-config ; then 
    :
  else
    AC_MSG_ERROR([ovdisis directory ($with_ovdisis) not present or not configured])
  fi

  # For use in oaesis-config
  ovdisis_cflags=`$with_ovdisis/ovdisis-config --cflags`
  ovdisis_libs=`$with_ovdisis/ovdisis-config --libs`

  ovdisis_release=`$with_ovdisis/ovdisis-config --version | sed 's%\\.[[0-9]]*$%%'`

  # canonicalize relative paths
  case $with_ovdisis in 
    /*)
      ovdisis_dir=$with_ovdisis
      ;;
    *)
      ovdisis_dir="\$(top_builddir)/$with_ovdisis"
      ;;
  esac

  OVDISIS_CFLAGS="-I$ovdisis_dir"
  OVDISIS_LIBS=$ovdisis_dir/libovdisis.la

  AC_SUBST(OVDISIS_CFLAGS)
  AC_SUBST(OVDISIS_LIBS)
fi

if test x$with_mintwrappers = xyes ; then
  AC_MSG_ERROR([
*** Directory must be specified for --with-mintwrappers])
fi

if test x$with_mintwrappers = x ; then 
  # Look for separately installed mintwrappers

  AM_PATH_MINTWRAPPERS(0.0.0,,
    AC_MSG_ERROR([
*** mintwrappers 0.0.0 or better is required. The latest version of mintwrappers
*** is always available from ftp://ftp.nocrew.org.]),
    libmint)

  mintwrappers_cflags=$MINTWRAPPERS_CFLAGS
  mintwrappers_libs=$MINTWRAPPERS_LIBS
else
  # Use uninstalled mintwrappers (assume they got the version right)

  if test -x $with_mintwrappers/mintwrappers-config ; then 
    :
  else
    AC_MSG_ERROR([mintwrappers directory ($with_mintwrappers) not present or not configured])
  fi

  # For use in oaesis-config
  mintwrappers_cflags=`$with_mintwrappers/mintwrappers-config --cflags`
  mintwrappers_libs=`$with_mintwrappers/mintwrappers-config --libs`

  mintwrappers_release=`$with_mintwrappers/mintwrappers-config --version | sed 's%\\.[[0-9]]*$%%'`

  # canonicalize relative paths
  case $with_mintwrappers in 
    /*)
      mintwrappers_dir=$with_mintwrappers
      ;;
    *)
      mintwrappers_dir="\$(top_builddir)/$with_mintwrappers"
      ;;
  esac

  MINTWRAPPERS_CFLAGS="-I$mintwrappers_dir"
  MINTWRAPPERS_LIBS=$mintwrappers_dir/libfb.la

  AC_SUBST(MINTWRAPPERS_CFLAGS)
  AC_SUBST(MINTWRAPPERS_LIBS)
fi

dnl This is needed for AC_CHECK_HEADERS to find the mintwrappers 
dnl and the oVDIsis headers
CPPFLAGS="$CPPFLAGS $MINTWRAPPERS_CFLAGS $OVDISIS_CFLAGS"

OSISINCLUDE=${includedir}/osis

dnl Set target specific things
case "${target}" in
    *mint*)    DEFS="$DEFS -DMINT_TARGET" ;;
esac

dnl Check for header files
AC_HEADER_STDC
AC_CHECK_HEADERS(alloc.h)
AC_CHECK_HEADERS(process.h)
AC_CHECK_HEADERS(support.h)
AC_CHECK_HEADERS(mintbind.h)
AC_CHECK_HEADERS(basepage.h)
AC_CHECK_HEADERS(compiler.h)
AC_CHECK_HEADERS(fcntl.h)
AC_CHECK_HEADERS(ioctl.h)
AC_CHECK_HEADERS(osbind.h)
AC_CHECK_HEADERS(vdi.h)
AC_CHECK_HEADERS(vdibind.h)
AC_CHECK_HEADERS(sysvars.h)

dnl Specify which variables that should be exchanged
AC_SUBST(PACKAGE)
AC_SUBST(VERSION)
AC_SUBST(LT_VERSION_INFO)
AC_SUBST(OSISINCLUDE)
AC_SUBST(target_cpu)
AC_SUBST(target_os)

dnl Set CFLAGS
CFLAGS="$CFLAGS -Wall -pedantic"

dnl Setup symbolic links
AC_LINK_FILES(src/common/debug.c, src/lib/debug.c)
AC_LINK_FILES(src/common/debug.c, src/server/debug.c)
AC_LINK_FILES(src/comm/srv_get_sockets.c, src/server/srv_get.c)
AC_LINK_FILES(src/comm/srv_put_sockets.c, src/lib/srv_put.c)
AC_LINK_FILES(src/common/vdi.c, src/lib/vdi.c)
AC_LINK_FILES(src/common/vdi.c, src/server/vdi.c)
AC_LINK_FILES(src/lib/resource.h, src/server/resource.h)

dnl Specify which files that should be created
AC_OUTPUT(Makefile src/Makefile src/comm/Makefile src/common/Makefile src/lib/Makefile src/server/Makefile test/Makefile oaesis.m4 oaesis-config)
